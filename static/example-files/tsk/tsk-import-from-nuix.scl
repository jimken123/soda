################################################################################
- <CaseName>      = 'CASE001'
- <CasePath>      = './ingest-filter'
- <Investigator>  = 'Antony'
- <Custodian>     = 'EDRM Micro'
- <Container>     = 'CASE01B00001'
- <ProductionSet> = 'TaggedItemsProduction'

- <Evidence> = [
    './data/EDRM specific data'
  ]

- <ExportPath> = PathCombine ['ConcordanceExport'] # Create absolute path

- <AutopsyCaseDirectory> = PathCombine ['AutopsyCases']

################################################################################

# Create a new case. It will remain open and will be automatically used by the other steps
- NuixCreateCase CaseName: <CaseName> CasePath: <CasePath> Investigator: <Investigator>

# Add evidence to the case
- NuixAddItem Custodian: <Custodian> Paths: <Evidence> Container: <Container>

# Create an item set for export
- NuixAddToItemSet ItemSetName: 'TaggedItems' SearchTerm: 'tag:*'

# Create a production set for export
- NuixAddToProductionSet
    ProductionSet: <ProductionSet>
    Search: $"item-set:TaggedItems"
    SearchType: SearchType.ItemsAndDescendants

# Export the production set as a concordance load file
- NuixExportConcordance
    ExportPath: <ExportPath>
    ProductionSet: <ProductionSet>
    TraversalStrategy: ExportTraversalStrategy.Items
    ExportOptions: (
      native: (path: 'NATIVE' naming: 'document_id')
      text: (path: 'TEXT' naming: 'document_id')
    )

- AutopsyCreateNewCase
    CaseName: <CaseName>
    CaseBaseDirectory: <AutopsyCaseDirectory>
    DataSourcePath: <ExportPath>
    IngestProfileName: ""
