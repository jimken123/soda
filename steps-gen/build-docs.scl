- <root> = 'steps'
- <summaryRegex> = '(?s)\.?\n.*$'
- <stepDescription> = 'Use this step to easily automate forensic and e-discovery workflows using Phoenix.'
- <enumDescription> = 'Enum value to help automate forensic and e-discovery workflows using Phoenix.'
- <connectors> = (
    Core: (
      Name: 'Core SDK'
      Description: 'An SDK for automating e-discovery and forensic workflows. Core is the SCL language grammar and interpreter, and an SDK for building connectors.'
    )
    FileSystem: (
      Name: 'File System Connector'
      Description: 'Phoenix connector for interacting with file systems of various OSes. Includes steps to interact with files, directories, paths, and compression.'
    )
    StructuredData: (
      Name: 'Structured Data Connector'
      Description: 'Phoenix connector for converting data between popular ediscovery and forensic structured data formats - Concordance, CSV, IDX, JSON, XML and YAML.'
    )
    Nuix: (
      Name: 'Nuix Connector'
      Description: 'Phoenix connector for Nuix. Easily automate workflows that create cases, ingest data, search, tag and filter, create item sets, export, and create reports.'
    )
    Sql: (
      Name: 'SQL Database Connector'
      Description: 'Phoenix connector for automating forensic and ediscovery workflows that use SQL databases. Supports MariaDb, MySQL, SQL Server, PostgreSQL, and SQLite.'
    )
    Singer: (
      Name: 'Singer Target'
      Description: 'Phoenix connector for Singer. Allows Phoenix to act as a singer target, receiving data from any of the 100+ singer taps.'
    )
    Pwsh: (
      Name: 'PowerShell Connector'
      Description: 'Phoenix connector for PowerShell. Use powershell scripts when automating forensic and ediscovery workflows.'
    )
    Relativity: (
      Name: 'Phoenix Connector for Relativity'
      Description: 'Easily include Relativity when automating forensic and ediscovery workflows. Create workspaces, matters, and folders, ingest and export data, search and tag documents.'
    )
    TSK: (
      Name: 'TSK Connector'
      Description: 'Phoenix connector for Autopsy / The Sleuth Kit. Include TSK in your automation workflows. Create cases, ingest data, generate reports, and list data sources.'
    )
    Tesseract: (
      Name: 'Tesseract OCR Connector'
      Description: 'Easily include Tesseract OCR in your forensic and ediscovery Phoenix automation workflows.'
    )
    Rest: (
      Name: 'REST Connector'
      Description: 'Automatically generate steps from any OpenAPI endpoint and include them in your forensic and ediscovery workflows with the Phoenix REST connector.'
    )
    Microsoft365: (
      Name: 'Microsoft 365 Connector'
      Description: 'Phoenix connector for Microsoft Graph API. Easily import and export data from Microsoft 365 applications when automating forensic and ediscovery workflows.'
    )
  )

- <pages> = (GenerateDocumentation).AllPages
- <pages> | ArrayDistinct (<>.Directory) | ForEach (
    If (<>.Directory != '') DirectoryCreate (PathCombine [<root>, <>.Directory])
  )
- <pages> | ForEach (
    - <conn> = <connectors>[<>.Category]
    - <title> = ''
    - <sidebar> = <>.Title
    - <description> = ''
    - <text> = RegexReplace In: <>.FileText Find: '^##' Replace: '#'
    - If (<>.PageType == 'Step')
        Then: (
          - <title> = $"{<>.StepName} Step | {<conn>.Name} Documentation"
          - <description> = $"{RegexReplace In: <>.Summary Find: <summaryRegex> Replace: ''}. {<stepDescription>}"
        )
        Else: If (<>.PageType == 'Contents') (
          - If (StringIsEmpty <>.Category)
            Then: (
              - <title> = $"All Phoenix Core and Connector Steps"
              - <sidebar> = 'All Phoenix Steps'
              - <description> = 'List of all steps for Phoenix, including Core and all official Connectors. Use these steps to automate forensic and e-discovery workflows.'
            )
            Else: (
              - <title> = $"All Steps | {<conn>.Name} Documentation"
              - <description> = $"List of all steps for {<conn>.Name}. {<conn>.Description}"
            )
          - <text> = RegexReplace In: <text> Find: '\]\(/' Replace: '](./'
        )
        Else: If (<>.PageType == 'Enums') (
          - <title> = $"{<>.Title} | Enums"
          - <description> = $"{RegexReplace In: <>.Summary Find: <summaryRegex> Replace: ''}. {<stepDescription>}"
        )
        Else: WriteStdErr $"Unknown Page {<>.PageType}"
    - <data> = StringJoin Delimiter: "\n" Strings: [
      "---"
      $"title: {<title>}"
      $"sidebar_label: {<sidebar>}"
      $"description: {<description>}"
      $"tags: [{<>.PageType}, {<>.Category}]"
      "hide_title: true"
      "hide_table_of_contents: true"
      "---\n"
      <text>
    ]
    - FileWrite
        Data: <data>
        Path: (PathCombine [<root>, <>.Directory, <>.FileName])
)
